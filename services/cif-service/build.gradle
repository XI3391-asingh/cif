plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id "org.sonarqube" version "3.3"
    id "com.github.johnrengelman.shadow" version "7.1.2"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "com.gorylenko.gradle-git-properties" version "2.4.1"
    id "org.openapi.generator" version "6.0.0"
}

group = 'com.cif.cifservice'
version = '1.0-SNAPSHOT'
description = 'CIF Microservice'

project.ext {
    dropwizardVersion = '2.1.0'
    mainClass = 'com.cif.cifservice.PartyServiceApplication'
    junitVersion = "5.8.2"
    testContainersVersion = "1.16.3"
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

gitProperties {
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "IST"
}

sourceSets {
    componentTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/component-test/java')
        }
        resources.srcDir file('src/component-test/resources')
    }
}

configurations {
    componentTestImplementation.extendsFrom testImplementation
    componentTestRuntime.extendsFrom testRuntime
}

task componentTest(type: Test) {
    testLogging {
        events("passed", "failed", "skipped")
    }
    description = "Runs component tests"
    testClassesDirs = sourceSets.componentTest.output.classesDirs
    classpath = sourceSets.componentTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

processComponentTestResources {
    duplicatesStrategy = 'include'
}

check.dependsOn componentTest
componentTest.mustRunAfter test

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "io.dropwizard:dropwizard-dependencies:${dropwizardVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testContainersVersion}"
    }
}

dependencies {
    implementation 'org.mapstruct:mapstruct:1.5.1.Final'
    implementation 'org.mapstruct:mapstruct-processor:1.5.1.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.1.Final'
    implementation 'io.dropwizard:dropwizard-core'
    implementation 'com.fasterxml.jackson.core:jackson-annotations'
    implementation 'jakarta.validation:jakarta.validation-api'
    implementation 'org.hibernate.validator:hibernate-validator'
    implementation 'io.dropwizard:dropwizard-jdbi3'
    implementation 'io.dropwizard:dropwizard-migrations'
    implementation 'org.jdbi:jdbi3-postgres'
    implementation 'org.jdbi:jdbi3-jackson2'
    runtimeOnly 'org.postgresql:postgresql:42.3.6'
    implementation 'io.swagger:swagger-annotations:1.6.6'
    implementation 'org.apache.kafka:kafka-clients:3.1.0'
    implementation 'io.dropwizard.metrics:metrics-core:4.2.10'
    implementation 'ru.vyarus.guicey:guicey-eventbus:5.6.1-1'
    implementation 'org.elasticsearch:elasticsearch:8.4.0'
    implementation 'org.json:json:20220320'
    implementation 'com.smoketurner:dropwizard-swagger:2.0.12-1'
    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-beanutils:commons-beanutils:1.9.4'
    implementation "com.github.vladimir-bukhtoyarov:bucket4j-hazelcast:7.0.0"
    implementation "javax.cache:cache-api:1.0.0"
    implementation "com.hazelcast:hazelcast:4.0.2"

    implementation group: 'org.apache.poi', name: 'poi', version: '3.17'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    implementation group: 'io.dropwizard', name: 'dropwizard-forms', version: '2.0.34'



    testImplementation("io.dropwizard:dropwizard-testing") {
        exclude group: "junit", module: "junit"
    }
    testImplementation("org.junit.jupiter:junit-jupiter")
    testImplementation("org.junit.jupiter:junit-jupiter-params")
    testImplementation("org.mockito:mockito-core")
    testImplementation("org.assertj:assertj-core")
    testImplementation("org.testcontainers:postgresql")
    testImplementation 'org.mockito:mockito-inline:4.8.0'
    componentTestImplementation("org.postgresql:postgresql:42.3.6")
    componentTestImplementation("org.testcontainers:junit-jupiter")
    componentTestImplementation("org.testcontainers:postgresql")
    componentTestImplementation("org.jbehave:jbehave-core:5.0")
    componentTestImplementation("io.dropwizard:dropwizard-client")
    componentTestImplementation("org.testcontainers:kafka:1.17.3")
    componentTestImplementation "org.testcontainers:elasticsearch:1.17.5"
}


shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA', 'META-INF/*.RSA', 'META-INF/*.SF'
    manifest {
        attributes 'Main-Class': mainClass
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "**/com/cif/cifservice/api/**",
                    "**/com/cif/cifservice/core/party/domain/**",
                    "**/com/cif/cifservice/core/party/util/**",
            ])
        }))
    }
}

test.finalizedBy jacocoTestReport

test {
    useJUnitPlatform()
    testLogging {
        events("passed", "failed", "skipped")
    }
}

tasks.withType(Checkstyle) {
    configFile file('codequality/checkstyle.xml')
    checkstyleMain.source = "src/main/java"
    checkstyleTest.enabled = false
    checkstyleComponentTest.enabled = false
}

build.finalizedBy shadowJar

sonarqube {
    properties {
        property "sonar.coverage.exclusions",
                ""
    }
}

abstract class ServiceJarSizeTask extends DefaultTask {
    @TaskAction
    def serviceJarSize() {
        def jarPath = project.buildDir.absolutePath + "/libs/${project.rootProject.name}-${project.version}-all.jar"
        def jarFile = project.file(jarPath)
        def jarSizeInMbs = (jarFile.length() / (1024 * 1024)).round(2)
        def maxJarSizeInMbs = 110
        if (jarSizeInMbs > maxJarSizeInMbs) {
            throw new GradleException("Service executable JAR size is $jarSizeInMbs Mb. The max allowed JAR size is $maxJarSizeInMbs Mb")
        } else {
            println "Service executable JAR size is $jarSizeInMbs Mb. It is below the current max jar size limit $maxJarSizeInMbs Mb"
        }
    }
}

tasks.register('serviceJarSize', ServiceJarSizeTask)

shadowJar.finalizedBy serviceJarSize

task buildApiServer(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "jaxrs-spec"
    inputSpec = "$rootDir/src/main/resources/cif-service-api.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "com.cif.cifservice.api"
    modelPackage = "com.cif.cifservice.api"
    configOptions = [
            java8            : "true",
            dateLibrary      : "java8",
            useBeanValidation: "true",
            interfaceOnly    : "true",
            returnResponse   : "true",
            generateBuilders : "true"
    ]
}

compileJava.dependsOn(buildApiServer)

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/gen/java"
        }
    }
}
